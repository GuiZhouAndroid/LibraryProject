plugins {
    id 'com.android.application'
}

//config.gradle根目录变量全局
def app = rootProject.ext.android
def dpc = rootProject.ext.dependencies

//构建配置
android {
    //命名空间对应当前module的全限定包名
    namespace 'zsdev.work.common_app'
    compileSdk app["compileSdk"]

    defaultConfig {
        applicationId "zsdev.work.common_app"
        minSdk app["minSdk"]
        targetSdk app["targetSdk"]
        versionCode app["versionCode"]
        versionName app["versionName"]

        //Java编译配置项
        javaCompileOptions {
            //注解配置
            annotationProcessorOptions {
                //启用注解功能
                //includeCompileClasspath true
                //AbstractProcessor中可以读取到该参数
                arguments = [moduleName: project.getName()]
            }
        }
    }

    //构建类型
    buildTypes {
        //开发版
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.1'
            //是否开启混淆
            minifyEnabled false
        }
        //发布版
        release {
            // R8 代码缩减功能
            minifyEnabled false
            //启用 R8 资源缩减功能
            //shrinkResources true
            //ProGuard规则：混淆文件列表，默认android混淆文件及本地proguard文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

//    //kotlin配置项
//    kotlinOptions {
//        //当compileOptions中指定Java版本为1.8时，需要设置兼容编译Kotlin
//        jvmTarget = '1.8'
//    }

    //开启dataBinding
    dataBinding {
        enabled = true
    }

    //编译java版本
    compileOptions {
        //将Java兼容性设置为Java 8  当前AndroidStudio使用的是JDK17，因此source+target需要设置为JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation platform(dpc["protobuf-java"])

    // kotlin 插件升级后避免开启dataBinding时出现kotlin-stdlib-jdk版本冲突
    implementation platform(dpc["kotlin-bom"])

    // Androidx
    implementation dpc["appcompat"]
    implementation dpc["constraintlayout"]
    implementation dpc["multidex"]

    //material
    implementation dpc["material"]

    //hilt
    implementation dpc["hilt-android"]
    annotationProcessor dpc["hilt-compiler"]

    //日志打印
    implementation dpc["logger"]

    //lombok
    compileOnly dpc["lombok"]

    //OkHttp3
    implementation dpc["okhttp"]
    implementation dpc["okio"]
    implementation dpc["logging-interceptor"]

    //retrofit2
    implementation(dpc["retrofit"]) {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        exclude group: 'com.squareup.okio', module: 'okio'
    }

    //rxjava3适配器
    implementation dpc["adapter-rxjava3"]

    //gson转换器最新版2.10.1
    implementation(dpc["converter-gson"]) {
        //内部封装了Gson2.8.5 这里不使用封装默认的低版本2.8.5版本，手动指定覆盖为最新版2.10.1
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation dpc["gson"]
    //moshi转换器
    implementation dpc["converter-moshi"]

    //AutoDispose
    implementation dpc["autodispose-android"]
    implementation dpc["autodispose-androidx-lifecycle"]


    // rxJava3
    implementation dpc["rxjava"]
    implementation dpc["rxandroid"]

    //弹窗
    implementation project(':lib_common')
}